import java.text.SimpleDateFormat

plugins {
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'net.nemerosa.versioning'         version '3.0.0'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

application.mainModule = 'eu.hansolo.evtbus'
mainClassName          = 'eu.hansolo.evtbus.example.Demo'
description = 'evtbus is a simple eventbus'

Date buildTimeAndDate = new Date()
ext {
    moduleName      = 'eu.hansolo.evtbus'
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')
    ciGHUser        = System.getenv('GH_USER')
    ciGHToken       = System.getenv('GH_TOKEN')
    gpgkey          = System.getenv("GPG_PRIVATE_KEY")
    gpgpassphrase   = System.getenv("PASSPHRASE")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'eu.hansolo:toolbox:17.0.29'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.evtbus',
                'Export-Package'        : 'eu.hansolo.evtbus, eu.hansolo.evt.bus.example',
                'Class-Path'            : '${project.name}-${project.version}.jar',
                'Main-Class'            : 'eu.hansolo.evtbus.example.Demo'
        )
    }
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    if (gpgkey && gpgpassphrase) {
        useInMemoryPgpKeys(gpgkey, gpgpassphrase)
    }

    sign configurations.archives
    sign publishing.publications
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            group      = 'eu.hansolo'
            artifactId = 'evtbus'
            version    = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml { }

            pom {
                name        = 'evtbus'
                description = '\'evtbus is a simple eventbus\''
                url         = "https://github.com/HanSolo/evtbus/wiki"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url  = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id    = "HanSolo"
                        name  = "Gerrit Grunwald"
                        email = "han.solo@mac.com"
                    }
                }
                scm {
                    connection          = "scm:git:https://HanSolo/evtbus"
                    developerConnection = "scm:git:https://HanSolo/evtbus"
                    url                 = "https://github.com/HanSolo/evtbus"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url  = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = null == ciOssrhUsername ? ossrhUsername : ciOssrhUsername
                password = null == ciOssrhPassword ? ossrhPassword : ciOssrhPassword
            }
        }
        maven {
            name = "GitHubPackages"
            url  = "https://maven.pkg.github.com/HanSolo/evtbus"
            credentials {
                username = null == ciGHUser  ? ghUser  : ciGHUser
                password = null == ciGHToken ? ghToken : ciGHToken
            }
        }
    }
}


// start the from gradle
task Main(type: JavaExec) {
    mainClass = "eu.hansolo.evtbus.example.Demo"
    classpath = sourceSets.main.runtimeClasspath
}


// Fix problems with loading resources
sourceSets {
    main {
        output.setResourcesDir(java.classesDirectory)
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}